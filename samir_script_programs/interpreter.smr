#########################
#LEXER
#########################

class Lexer {
    var pos = 0;
    var source: string;
    var tokens: List;
    var current: string;

    func _init(source: string) {
        self.source = source;
        pos = -1;
        advance();
        tokens = List();
    }

    func lex(){

        while current != "\0" do {

            # Ignore whitespace:
            if current == " " then
                advance();
            
            else if current == "+" then{
                add_token(TT_PLUS, "+");
                advance();
            }
            else if current == "-" then{
                add_token(TT_MINUS, "-");
                advance();
            }
            else if current == "*" then{
                add_token(TT_MULTIPLY, "*");
                advance();
            }
            else if current == "/" then{
                add_token(TT_DIV, "/");
                advance();
            }
            else if current == "(" then {
                add_token(TT_L_PAREN, "(");
                advance();
            }
            else if current == ")" then {
                add_token(TT_R_PAREN, ")");
                advance();
            }
                
            else if(isNumeric(current)) then {
                make_number();
            }
            
            else 
                return Error(current + " is an invalid char");
            
        }

        stop();
        var end_token: Token = Token(TT_EOF, "EOF");
        tokens.add(end_token);
        return tokens;
    }

    func advance(){
        if pos + 1 >= len(source) then{
            pos = pos + 1;
            current = "\0";
        }
        else{
            pos = pos + 1;
            current = getChar(pos, source);
        }
        
    }

    func peek_next(){
        if pos + 1 >= len(source) then
            return "\0";
        return getChar(pos + 1, source);
    }

    func add_token(type: number, value: string){
        var token: Token = Token(type, value);
        tokens.add(token);
    }

    func make_number(){
        var number_string: string = "";
        while isNumeric(current) do {
            number_string = number_string + current;
            advance();
        }
        add_token(TT_NUMBER, num(number_string));
    }

}

#########################
#TOKENS AND TOKEN TYPES
#########################

var TT_NUMBER = 0;
var TT_PLUS = 1;
var TT_MINUS = 2;
var TT_MULTIPLY = 3;
var TT_DIV = 4;
var TT_L_PAREN = 5;
var TT_R_PAREN = 6;
var TT_EOF;

class Token {
    var type: number;
    var value: string;

    func _init(type: number, value: string){
        self.type = type;
        self.value = value;
    }

    func _toString() -> string{
        return str(value);
    }

}

#########################
#PARSER
#########################

class Parser {

    var tokens: List;
    var current: Token;
    var pos: number;

    var hadError: boolean = false;
    var log: string;

    func _init(tokens: List){
        self.tokens = tokens;
        current = tokens.get(0);
        pos = 0;
    }

    func parse() -> Node {
        return expression();
    }

    func expression() -> Node {
        var left: Node = term();

        while current.type == TT_MINUS or
        current.type == TT_PLUS do {
            var op_tok: Token = current;
            advance();
            var right: Node = term();
            left = BinOpNode(left, op_tok, right);
        }

        return left;

    }

    func term() -> Node {
        var left: Node = unary();

        while current.type == TT_MULTIPLY or
        current.type == TT_DIV do {
            var op_tok: Token = current;
            advance();
            var right: Node = unary();
            left = BinOpNode(left, op_tok, right);
        }

        return left;
    }

    func unary() -> Node {
        if current.type == TT_MINUS then {
            var op: string = "-";
            advance();
            var right: Node = unary();
            return UnaryOpNode(op, right);
        }

        return primary();
    }

    func primary() -> Node {
        if current.type == TT_NUMBER then {
            var node: Node = NumberNode(current.value);
            advance();
            return node;
        }

        elif current.type == TT_L_PAREN then {
            advance();
            var expre: Node = expression();
            if current.type != TT_R_PAREN then{
                hadError = true;
                log = "Expected closing ')'";
            }
            advance();
            return GroupingNode(expre);
        }

        hadError = true;
        log = "Invalid syntax";
        return nil;
    }

    func advance(){
        if pos + 1 >= tokens.size then{
            hadError = true;
            log = "invalid syntax";
            return;
        }
        pos = pos + 1;
        current = tokens.get(pos);
    }

}

#########################
# AST NODES
#########################

class NumberNode {
    var value: number;

    func _init(value: number){
        self.value = value;
    }

    func _toString() -> string {
        return str(value);
    }

    func visit(){
        return value;
    }
}

class GroupingNode {
    var expre: Node;

    func _init(expre: Node){
        self.expre = expre;
    }

    func _toString() -> string {
        return "(" + str(expre) + ")";
    }

    func visit() -> variant {
        return expre.visit();
    }

    
}

class BinOpNode {
    var op;
    var left;
    var right;

    func _init(left: variant, op: Token, right: variant){
        self.left = left;
        self.right = right;
        self.op = op;
    }

    func _toString() -> string{
        return "(" + str(left) + str(op) + str(right) + ")";
    }

    func visit() -> number {
        var right_op = right.visit();
        var left_op = left.visit();

        if op.type == TT_PLUS then return left_op + right_op;
        if op.type == TT_MINUS then return left_op - right_op;
        if op.type == TT_MULTIPLY then return left_op * right_op;
        if op.type == TT_DIV then return left_op / right_op;

    }
}

class UnaryOpNode {
    var op: Token;
    var right: Node;

    func _init(op: Token, right: Node){
        self.op = op;
        self.right = right;
    }

    func visit() -> number {
        if op == "-" then
            return right.visit() * -1;    
    }

    func _toString() -> string {
        return str(op) + "(" + str(right) + ")" ;
    }

}

#########################
#ERROR HANDLING
#########################

class Error {
    var log: string;
    func _init(log: string){
        self.log = log;
    }
}

#########################
#ENTRY POINT
#########################


while true do {

    var source: string = input("> ");

    var lexer: Lexer = Lexer(source);
    var tokens: List = lexer.lex();
    if typeOf(tokens) == "Error" then {
        println tokens.log;
        continue;}

    var parser: Parser = Parser(tokens);
    var AST: Node = parser.parse();
    if parser.hadError then {
        println parser.log;
        continue;}

    println AST.visit();

}