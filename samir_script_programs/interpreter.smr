#########################
#LEXER
#########################

class Lexer {
    var pos = 0;
    var source;
    var tokens;
    var current;

    func _init(source){
        self.source = source;
        pos = -1;
        advance();
        tokens = List();
    }

    func lex(){

        while current != "\0" do {

            # Ignore whitespace:
            if current == " " then
                advance();
            
            else if current == "+" then{
                add_token(TT_PLUS, "+");
                advance();
            }
            else if current == "-" then{
                add_token(TT_MINUS, "-");
                advance();
            }
            else if current == "*" then{
                add_token(TT_MULTIPLY, "*");
                advance();
            }
            else if current == "/" then{
                add_token(TT_DIV, "/");
                advance();
            }
            else if current == "(" then {
                add_token(TT_L_PAREN, "(");
                advance();
            }
            else if current == ")" then {
                add_token(TT_R_PAREN, ")");
                advance();
            }
                
            else if(isNumeric(current)) then {
                make_number();
            }
            
            else 
                return Error(current + " is an invalid char");
            
        }

        var end_token = Token(TT_EOF, "EOF");
        tokens.add(end_token);
        return tokens;
    }

    func advance(){
        if pos + 1 >= len(source) then{
            pos = pos + 1;
            current = "\0";
        }
        else{
            pos = pos + 1;
            current = getChar(pos, source);
        }
        
    }

    func peek_next(){
        if pos + 1 >= len(source) then
            return "\0";
        return getChar(pos + 1, source);
    }

    func add_token(type, value){
        var token = Token(type, value);
        tokens.add(token);
    }

    func make_number(){
        var number_string = "";
        while isNumeric(current) do {
            number_string = number_string + current;
            advance();
        }
        add_token(TT_NUMBER, num(number_string));
    }

}

#########################
#TOKENS AND TOKEN TYPES
#########################

var TT_NUMBER = 0;
var TT_PLUS = 1;
var TT_MINUS = 2;
var TT_MULTIPLY = 3;
var TT_DIV = 4;
var TT_L_PAREN = 5;
var TT_R_PAREN = 6;
var TT_EOF;

class Token {
    var type;
    var value;

    func _init(type, value){
        self.type = type;
        self.value = value;
    }

    func _toString(){
        return str(value);
    }

}

#########################
#PARSER
#########################

class Parser {

    var tokens;
    var current;
    var pos;

    var hadError = false;
    var log;

    func _init(tokens){
        self.tokens = tokens;
        current = tokens.get(0);
        pos = 0;
    }

    func parse(){
        return expression();
    }

    func expression(){
        var left = term();

        while current.type == TT_MINUS or
        current.type == TT_PLUS do {
            var op_tok = current;
            advance();
            var right = term();
            left = BinOpNode(left, op_tok, right);
        }

        return left;

    }

    func term(){
        var left = unary();

        while current.type == TT_MULTIPLY or
        current.type == TT_DIV do {
            var op_tok = current;
            advance();
            var right = unary();
            left = BinOpNode(left, op_tok, right);
        }

        return left;
    }

    func unary(){
        if current.type == TT_MINUS then {
            var op = "-";
            advance();
            var right = unary();
            return UnaryOpNode(op, right);
        }

        return primary();
    }

    func primary(){
        if current.type == TT_NUMBER then {
            var node = NumberNode(current.value);
            advance();
            return node;
        }

        elif current.type == TT_L_PAREN then {
            advance();
            var expre = expression();
            if current.type != TT_R_PAREN then{
                hadError = true;
                log = "Expected closing ')'";
            }
            advance();
            return GroupingNode(expre);
        }

        hadError = true;
        log = "Invalid syntax";
        return nil;
    }

    func advance(){
        if pos + 1 >= tokens.size then{
            hadError = true;
            log = "invalid syntax";
            return;
        }
        pos = pos + 1;
        current = tokens.get(pos);
    }

}

#########################
# AST NODES
#########################

class NumberNode {
    var value;

    func _init(value){
        self.value = value;
    }

    func _toString(){
        return str(value);
    }

    func visit(){
        return value;
    }
}

class GroupingNode {
    var expre;

    func _init(expre){
        self.expre = expre;
    }

    func _toString(){
        return "(" + str(expre) + ")";
    }

    func visit(){
        return expre.visit();
    }

    
}

class BinOpNode {
    var op;
    var left;
    var right;

    func _init(left, op, right){
        self.left = left;
        self.right = right;
        self.op = op;
    }

    func _toString(){
        return "(" + str(left) + str(op) + str(right) + ")";
    }

    func visit(){
        var right_op = right.visit();
        var left_op = left.visit();

        if op.type == TT_PLUS then return left_op + right_op;
        if op.type == TT_MINUS then return left_op - right_op;
        if op.type == TT_MULTIPLY then return left_op * right_op;
        if op.type == TT_DIV then return left_op / right_op;

    }
}

class UnaryOpNode {
    var op;
    var right;

    func _init(op, right){
        self.op = op;
        self.right = right;
    }

    func visit(){
        if op == "-" then
            return right.visit() * -1;    
    }

    func _toString(){
        return str(op) + "(" + str(right) + ")" ;
    }

}

#########################
#ERROR HANDLING
#########################

class Error {
    var log;
    func _init(log){
        self.log = log;
    }
}

#########################
#ENTRY POINT
#########################


while true do {

    var source = input("> ");

    var lexer = Lexer(source);
    var tokens = lexer.lex();
    if typeOf(tokens) == "Error" then {
        println tokens.log;
        continue;}

    var parser = Parser(tokens);
    var AST = parser.parse();
    if parser.hadError then {
        println parser.log;
        continue;}

    println AST.visit();

}